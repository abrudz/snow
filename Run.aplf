 {tid}←Run arg
 ;r;c;snow;star;tree;moon;Put;sky;flakes;window;mask;ULine;had

 :If 0=⎕TID
     tid←Run&arg ⋄ :Return
 :EndIf

 r←30 ⋄ c←80 ⋄ snow←∊,\'XO∆' ⋄ star←∊,\'·∙•' ⋄ tree←'⍋' ⋄ moon←'()'

⍝ open background editor as fn (for colours)
 ⎕ED&'window' ⋄ ⎕NQ ⎕SE'GotFocus' ⍝ go back to session
 ⍞←'⎕TKILL',⎕TID,'⍝ Press Enter to stop'

 Put←{(⍺,' ')[l⌊?c⍴⍵×l←1+≢⍺]} ⍝ ⍵-spread-out random ⍺ chars
 ULine←{⊃⎕FMT'_'(⎕UCS 8)⍵}¨   ⍝ combine chars with underline
 :Repeat ⍝ set vars
     sky←moon[?≢moon]@(⊂?c,⍨r-1)⊢(↑star∘Put¨40⍴⍨r-1)⍪(tree Put 5)
     flakes←r c⍴'' ⋄ had←c⍴0

     :Repeat
         flakes((snow Put 30)⍪¯1↓⌽⍨)←1⌊¯2+?r⍴7

         mask←(tree≠sky)∧(' '≠flakes)
         window←ULine@{had}@r⊢(mask/⍥,flakes)@{mask}sky
         had∨←(' '≠⊢⌿flakes)∧(tree≠⊢⌿sky)

         ⎕DL÷8
     :Until ~' '∊⊢⌿window
 :EndRepeat
